load_file("nrngui.hoc")

// load my cell template
load_file("ball_and_stick.hoc")

// simulation setting
NCELL = 10
tstop = 200
dt = 0.0005

objref cells, nclist
objref stim, ncstim
objref tvec, idvec
objref myplot

proc makeCells() { local i, num_cell localobj cell
    num_cell = $1
    cells = new List()
    for i=0, num_cell-1 {
	cell = new B_BallStick(0, 20*i, 0)
	cells.append(cell)
    }
}

proc connectCells() { local i localobj src, target, syn, nc
    nclist = new List()
    for i=0, cells.count-1 {
	src = cells.object(i)
	target = cells.object((i+1)%cells.count)
	syn = target.synlist.object(0)

	nc = src.connect2target(syn)
	nclist.append(nc)
	nc.delay = 1
	nc.weight = 0.01
    }
}

proc setStim() {
    stim = new NetStim()
    stim.number = 1
    stim.start = 0
    ncstim = new NetCon(stim, cells.object(0).synlist.object(0))
    ncstim.delay = 0
    ncstim.weight = 0.01
}

proc setSpikeRecorder() { local i localobj nc, nil
    tvec = new Vector()
    idvec = new Vector()
    for i=0, nclist.count-1 {
		nc = cells.object(i).connect2target(nil)
		nc.record(tvec, idvec, i)
    }
}

proc printSpike() { local i
    printf("\ntime\t cell\n")
    for i=0, tvec.size-1 {
	printf("%g\t %d\n", tvec.x[i], idvec.x[i])
    }
}

proc setPlot() {
    myplot = new PlotShape()
    myplot.show(0)
    myplot.exec_menu("Shape Plot")
    flush_list.append(myplot)
}

proc start(){
    run()
    printSpike()
}

makeCells(NCELL)
connectCells()
setStim()

setSpikeRecorder()
setPlot()

//quit()
